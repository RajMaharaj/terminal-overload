// Copyright information can be found in the file named COPYING
// located in the root directory of this distribution.

$XaNotc1EtherformMoveMap_RemapCount = 0;
$XaNotc1EtherformMoveMap_RemapName[$XaNotc1EtherformMoveMap_RemapCount] = "Fly Forward";
$XaNotc1EtherformMoveMap_RemapCmd[$XaNotc1EtherformMoveMap_RemapCount] = "XaNotc1CatMoveMap_moveforward";
$XaNotc1EtherformMoveMap_RemapCount++;
$XaNotc1EtherformMoveMap_RemapName[$XaNotc1EtherformMoveMap_RemapCount] = "Fly Backward";
$XaNotc1EtherformMoveMap_RemapCmd[$XaNotc1EtherformMoveMap_RemapCount] = "XaNotc1CatMoveMap_movebackward";
$XaNotc1EtherformMoveMap_RemapCount++;
$XaNotc1EtherformMoveMap_RemapName[$XaNotc1EtherformMoveMap_RemapCount] = "Fly Left";
$XaNotc1EtherformMoveMap_RemapCmd[$XaNotc1EtherformMoveMap_RemapCount] = "XaNotc1CatMoveMap_moveleft";
$XaNotc1EtherformMoveMap_RemapCount++;
$XaNotc1EtherformMoveMap_RemapName[$XaNotc1EtherformMoveMap_RemapCount] = "Fly Right";
$XaNotc1EtherformMoveMap_RemapCmd[$XaNotc1EtherformMoveMap_RemapCount] = "XaNotc1CatMoveMap_moveright";
$XaNotc1EtherformMoveMap_RemapCount++;
$XaNotc1EtherformMoveMap_RemapName[$XaNotc1EtherformMoveMap_RemapCount] = "Fly Up";
$XaNotc1EtherformMoveMap_RemapCmd[$XaNotc1EtherformMoveMap_RemapCount] = "XaNotc1CatMoveMap_jump";
$XaNotc1EtherformMoveMap_RemapCount++;
$XaNotc1EtherformMoveMap_RemapName[$XaNotc1EtherformMoveMap_RemapCount] = "Fly Down";
$XaNotc1EtherformMoveMap_RemapCmd[$XaNotc1EtherformMoveMap_RemapCount] = "XaNotc1CatMoveMap_doSlide";
$XaNotc1EtherformMoveMap_RemapCount++;
$XaNotc1EtherformMoveMap_RemapName[$XaNotc1EtherformMoveMap_RemapCount] = "Materialize selected class";
$XaNotc1EtherformMoveMap_RemapCmd[$XaNotc1EtherformMoveMap_RemapCount] = "XaNotc1EtherformMoveMap_manifest";
$XaNotc1EtherformMoveMap_RemapCount++;
$XaNotc1EtherformMoveMap_RemapName[$XaNotc1EtherformMoveMap_RemapCount] = "Select class #1";
$XaNotc1EtherformMoveMap_RemapCmd[$XaNotc1EtherformMoveMap_RemapCount] = "XaNotc1EtherformMoveMap_selectLoadout0";
$XaNotc1EtherformMoveMap_RemapCount++;
$XaNotc1EtherformMoveMap_RemapName[$XaNotc1EtherformMoveMap_RemapCount] = "Select class #2";
$XaNotc1EtherformMoveMap_RemapCmd[$XaNotc1EtherformMoveMap_RemapCount] = "XaNotc1EtherformMoveMap_selectLoadout1";
$XaNotc1EtherformMoveMap_RemapCount++;
$XaNotc1EtherformMoveMap_RemapName[$XaNotc1EtherformMoveMap_RemapCount] = "Select class #3";
$XaNotc1EtherformMoveMap_RemapCmd[$XaNotc1EtherformMoveMap_RemapCount] = "XaNotc1EtherformMoveMap_selectLoadout2";
$XaNotc1EtherformMoveMap_RemapCount++;
$XaNotc1EtherformMoveMap_RemapName[$XaNotc1EtherformMoveMap_RemapCount] = "Select class #4";
$XaNotc1EtherformMoveMap_RemapCmd[$XaNotc1EtherformMoveMap_RemapCount] = "XaNotc1EtherformMoveMap_selectLoadout3";
$XaNotc1EtherformMoveMap_RemapCount++;
$XaNotc1EtherformMoveMap_RemapName[$XaNotc1EtherformMoveMap_RemapCount] = "Select class #5";
$XaNotc1EtherformMoveMap_RemapCmd[$XaNotc1EtherformMoveMap_RemapCount] = "XaNotc1EtherformMoveMap_selectLoadout4";
$XaNotc1EtherformMoveMap_RemapCount++;
$XaNotc1EtherformMoveMap_RemapName[$XaNotc1EtherformMoveMap_RemapCount] = "Select class #6";
$XaNotc1EtherformMoveMap_RemapCmd[$XaNotc1EtherformMoveMap_RemapCount] = "XaNotc1EtherformMoveMap_selectLoadout5";
$XaNotc1EtherformMoveMap_RemapCount++;
$XaNotc1EtherformMoveMap_RemapName[$XaNotc1EtherformMoveMap_RemapCount] = "Select class #7";
$XaNotc1EtherformMoveMap_RemapCmd[$XaNotc1EtherformMoveMap_RemapCount] = "XaNotc1EtherformMoveMap_selectLoadout6";
$XaNotc1EtherformMoveMap_RemapCount++;
$XaNotc1EtherformMoveMap_RemapName[$XaNotc1EtherformMoveMap_RemapCount] = "Select class #8";
$XaNotc1EtherformMoveMap_RemapCmd[$XaNotc1EtherformMoveMap_RemapCount] = "XaNotc1EtherformMoveMap_selectLoadout7";
$XaNotc1EtherformMoveMap_RemapCount++;
$XaNotc1EtherformMoveMap_RemapName[$XaNotc1EtherformMoveMap_RemapCount] = "Select class #9";
$XaNotc1EtherformMoveMap_RemapCmd[$XaNotc1EtherformMoveMap_RemapCount] = "XaNotc1EtherformMoveMap_selectLoadout8";
$XaNotc1EtherformMoveMap_RemapCount++;
$XaNotc1EtherformMoveMap_RemapName[$XaNotc1EtherformMoveMap_RemapCount] = "Select class #10";
$XaNotc1EtherformMoveMap_RemapCmd[$XaNotc1EtherformMoveMap_RemapCount] = "XaNotc1EtherformMoveMap_selectLoadout9";
$XaNotc1EtherformMoveMap_RemapCount++;

function XaNotcSettings1_EtherformControlsGui::onWake(%this)
{
   //echo("XaNotcSettings1_EtherformControlsGui::onWake");
   
   if($InGuiEditor)
      return;

   %this-->MouseSensitivity.value = $pref::Input::LinkMouseSensitivity;
   %this.fillRemapList();
}

function XaNotcSettings1_EtherformControlsGui::onSleep(%this)
{
   //echo("XaNotcSettings1_EtherformControlsGui::onSleep");

   // write out the control config into the rw config file
   XaNotc1EtherformMoveMap.save($SettingsDir@"/XaNotc1EtherformMoveMap.cs");
}

function XaNotcSettings1_EtherformControlsGui::fillRemapList( %this )
{
   %remapList = %this-->RemapList;

	%remapList.clear();
   for ( %i = 0; %i < $XaNotc1EtherformMoveMap_RemapCount; %i++ )
      %remapList.addRow( %i, XaNotcSettings1_EtherformControls_buildFullMapString( %i ) );
}

function XaNotcSettings1_EtherformControlsGui::doRemap( %this )
{
   %remapList = %this-->RemapList;

	%selId = %remapList.getSelectedId();
   %name = $XaNotc1EtherformMoveMap_RemapName[%selId];

	XaNotcSettings1_Controls_RemapDlg-->OptRemapText.setValue( "Re-bind \"" @ %name @ "\" to..." );
	XaNotcSettings1_Controls_RemapInputCtrl.index = %selId;
   XaNotcSettings1_Controls_RemapInputCtrl.inputEventCallback = "XaNotcSettings1_EtherformControls_onRemapCtrlInputEvent";
	Canvas.pushDialog(XaNotcSettings1_Controls_RemapDlg);
}

function XaNotcSettings1_EtherformControls_onRemapCtrlInputEvent(%device, %action)
{
   //error( "** onInputEvent called - device = " @ %device @ ", action = " @ %action @ " **" );
   Canvas.popDialog(XaNotcSettings1_Controls_RemapDlg);
   
   %index = XaNotcSettings1_Controls_RemapInputCtrl.index;

   // Test for the reserved keystrokes:
   if ( %device $= "keyboard" )
   {
      // Cancel...
      if ( %action $= "escape" )
      {
         // Do nothing...
         return;
      }
   }

   %cmd  = $XaNotc1EtherformMoveMap_RemapCmd[%index];
   %name = $XaNotc1EtherformMoveMap_RemapName[%index];

   // Grab the friendly display name for this action
   // which we'll use when prompting the user below.
   %mapName = XaNotcSettings1_EtherformControls_getMapDisplayName( %device, %action );

   // Get the current command this action is mapped to.
   %prevMap = XaNotc1EtherformMoveMap.getCommand( %device, %action );

   // If nothing was mapped to the previous command
   // mapping then it's easy... just bind it.
   if ( %prevMap $= "" )
   {
      XaNotcSettings1_EtherformControls_unbindExtraActions( %cmd, 1 );
      XaNotc1EtherformMoveMap.bind( %device, %action, %cmd );
      optionsDlg-->RemapList.setRowById( %index, XaNotcSettings1_EtherformControls_buildFullMapString( %index ) );
      return;
   }

   // If the previous command is the same as the
   // current then they hit the same input as what
   // was already assigned.
   if ( %prevMap $= %cmd )
   {
      XaNotcSettings1_EtherformControls_unbindExtraActions( %cmd, 0 );
      XaNotc1EtherformMoveMap.bind( %device, %action, %cmd );
      optionsDlg-->RemapList.setRowById( %index, XaNotcSettings1_EtherformControls_buildFullMapString( %index ) );
      return;
   }

   // Look for the index of the previous mapping.
   %prevMapIndex = XaNotcSettings1_EtherformControls_findRemapCmdIndex( %prevMap );

   // If we get a negative index then the previous
   // mapping was to an item that isn't included in
   // the mapping list... so we cannot unmap it.
   if ( %prevMapIndex == -1 )
   {
      MessageBoxOK( "Remap Failed", "\"" @ %mapName @ "\" is already bound to a non-remappable command!" );
      return;
   }

   // Setup the forced remapping callback command.
   %callback = "XaNotcSettings1_EtherformControls_redoMapping(" @ %device @ ", \"" @ %action @ "\", \"" @
                              %cmd @ "\", " @ %prevMapIndex @ ", " @ %index @ ");";

   // Warn that we're about to remove the old mapping and
   // replace it with another.
   %prevCmdName = $XaNotc1EtherformMoveMap_RemapName[%prevMapIndex];
   MessageBoxYesNo( "Warning",
      "\"" @ %mapName @ "\" is already bound to \""
      @ %prevCmdName @ "\"!\nDo you wish to replace this mapping?",
       %callback, "" );
}

function XaNotcSettings1_EtherformControls_getMapDisplayName( %device, %action )
{
	if ( %device $= "keyboard" )
		return( %action );
	else if ( strstr( %device, "mouse" ) != -1 )
	{
		// Substitute "mouse" for "button" in the action string:
		%pos = strstr( %action, "button" );
		if ( %pos != -1 )
		{
			%mods = getSubStr( %action, 0, %pos );
			%object = getSubStr( %action, %pos, 1000 );
			%instance = getSubStr( %object, strlen( "button" ), 1000 );
			return( %mods @ "mouse" @ ( %instance + 1 ) );
		}
		else
			error( "Mouse input object other than button passed to getDisplayMapName!" );
	}
	else if ( strstr( %device, "joystick" ) != -1 )
	{
		// Substitute "joystick" for "button" in the action string:
		%pos = strstr( %action, "button" );
		if ( %pos != -1 )
		{
			%mods = getSubStr( %action, 0, %pos );
			%object = getSubStr( %action, %pos, 1000 );
			%instance = getSubStr( %object, strlen( "button" ), 1000 );
			return( %mods @ "joystick" @ ( %instance + 1 ) );
		}
		else
	   {
	      %pos = strstr( %action, "pov" );
         if ( %pos != -1 )
         {
            %wordCount = getWordCount( %action );
            %mods = %wordCount > 1 ? getWords( %action, 0, %wordCount - 2 ) @ " " : "";
            %object = getWord( %action, %wordCount - 1 );
            switch$ ( %object )
            {
               case "upov":   %object = "POV1 up";
               case "dpov":   %object = "POV1 down";
               case "lpov":   %object = "POV1 left";
               case "rpov":   %object = "POV1 right";
               case "upov2":  %object = "POV2 up";
               case "dpov2":  %object = "POV2 down";
               case "lpov2":  %object = "POV2 left";
               case "rpov2":  %object = "POV2 right";
               default:       %object = "??";
            }
            return( %mods @ %object );
         }
         else
            error( "Unsupported Joystick input object passed to getDisplayMapName!" );
      }
	}

	return( "??" );
}

function XaNotcSettings1_EtherformControls_buildFullMapString( %index )
{
   %name       = $XaNotc1EtherformMoveMap_RemapName[%index];
   %cmd        = $XaNotc1EtherformMoveMap_RemapCmd[%index];

   %temp = XaNotc1EtherformMoveMap.getBinding( %cmd );
   if ( %temp $= "" )
      return %name TAB "";

   %mapString = "";

   %count = getFieldCount( %temp );
   for ( %i = 0; %i < %count; %i += 2 )
   {
      if ( %mapString !$= "" )
         %mapString = %mapString @ ", ";

      %device = getField( %temp, %i + 0 );
      %object = getField( %temp, %i + 1 );
      %mapString = %mapString @ XaNotcSettings1_EtherformControls_getMapDisplayName( %device, %object );
   }

   return %name TAB %mapString;
}

function XaNotcSettings1_EtherformControls_redoMapping( %device, %action, %cmd, %oldIndex, %newIndex )
{
	//%actionMap.bind( %device, %action, $XaNotc1EtherformMoveMap_RemapCmd[%newIndex] );
	XaNotc1EtherformMoveMap.bind( %device, %action, %cmd );

   %remapList = XaNotcSettings1_EtherformControlsGui-->RemapList;
	%remapList.setRowById( %oldIndex, XaNotcSettings1_EtherformControls_buildFullMapString( %oldIndex ) );
	%remapList.setRowById( %newIndex, XaNotcSettings1_EtherformControls_buildFullMapString( %newIndex ) );
}

function XaNotcSettings1_EtherformControls_findRemapCmdIndex( %command )
{
	for ( %i = 0; %i < $XaNotc1EtherformMoveMap_RemapCount; %i++ )
	{
		if ( %command $= $XaNotc1EtherformMoveMap_RemapCmd[%i] )
			return( %i );
	}
	return( -1 );
}

/// This unbinds actions beyond %count associated to the
/// particular XaNotc1EtherformMoveMap %commmand.
function XaNotcSettings1_EtherformControls_unbindExtraActions( %command, %count )
{
   %temp = XaNotc1EtherformMoveMap.getBinding( %command );
   if ( %temp $= "" )
      return;

   %count = getFieldCount( %temp ) - ( %count * 2 );
   for ( %i = 0; %i < %count; %i += 2 )
   {
      %device = getField( %temp, %i + 0 );
      %action = getField( %temp, %i + 1 );

      XaNotc1EtherformMoveMap.unbind( %device, %action );
   }
}

function XaNotcSettings1_EtherformControls_restoreDefaultMappings()
{
   XaNotc1EtherformMoveMap.delete();
   exec($SettingsDir@"/XaNotc1EtherformMoveMap.cs");
   optionsDlg.fillRemapList();
}


