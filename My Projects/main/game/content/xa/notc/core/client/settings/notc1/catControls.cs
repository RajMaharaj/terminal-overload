// Copyright information can be found in the file named COPYING
// located in the root directory of this distribution.

$XaNotc1CatMoveMap_RemapCount = 0;
$XaNotc1CatMoveMap_RemapName[$XaNotc1CatMoveMap_RemapCount] = "Forward";
$XaNotc1CatMoveMap_RemapCmd[$XaNotc1CatMoveMap_RemapCount] = "XaNotc1CatMoveMap_moveforward";
$XaNotc1CatMoveMap_RemapCount++;
$XaNotc1CatMoveMap_RemapName[$XaNotc1CatMoveMap_RemapCount] = "Backward";
$XaNotc1CatMoveMap_RemapCmd[$XaNotc1CatMoveMap_RemapCount] = "XaNotc1CatMoveMap_movebackward";
$XaNotc1CatMoveMap_RemapCount++;
$XaNotc1CatMoveMap_RemapName[$XaNotc1CatMoveMap_RemapCount] = "Strafe Left";
$XaNotc1CatMoveMap_RemapCmd[$XaNotc1CatMoveMap_RemapCount] = "XaNotc1CatMoveMap_moveleft";
$XaNotc1CatMoveMap_RemapCount++;
$XaNotc1CatMoveMap_RemapName[$XaNotc1CatMoveMap_RemapCount] = "Strafe Right";
$XaNotc1CatMoveMap_RemapCmd[$XaNotc1CatMoveMap_RemapCount] = "XaNotc1CatMoveMap_moveright";
$XaNotc1CatMoveMap_RemapCount++;
$XaNotc1CatMoveMap_RemapName[$XaNotc1CatMoveMap_RemapCount] = "Turn Left";
$XaNotc1CatMoveMap_RemapCmd[$XaNotc1CatMoveMap_RemapCount] = "XaNotc1CatMoveMap_turnLeft";
$XaNotc1CatMoveMap_RemapCount++;
$XaNotc1CatMoveMap_RemapName[$XaNotc1CatMoveMap_RemapCount] = "Turn Right";
$XaNotc1CatMoveMap_RemapCmd[$XaNotc1CatMoveMap_RemapCount] = "XaNotc1CatMoveMap_turnRight";
$XaNotc1CatMoveMap_RemapCount++;
$XaNotc1CatMoveMap_RemapName[$XaNotc1CatMoveMap_RemapCount] = "Look Up";
$XaNotc1CatMoveMap_RemapCmd[$XaNotc1CatMoveMap_RemapCount] = "XaNotc1CatMoveMap_panUp";
$XaNotc1CatMoveMap_RemapCount++;
$XaNotc1CatMoveMap_RemapName[$XaNotc1CatMoveMap_RemapCount] = "Look Down";
$XaNotc1CatMoveMap_RemapCmd[$XaNotc1CatMoveMap_RemapCount] = "XaNotc1CatMoveMap_panDown";
$XaNotc1CatMoveMap_RemapCount++;
$XaNotc1CatMoveMap_RemapName[$XaNotc1CatMoveMap_RemapCount] = "Jump";
$XaNotc1CatMoveMap_RemapCmd[$XaNotc1CatMoveMap_RemapCount] = "XaNotc1CatMoveMap_jump";
$XaNotc1CatMoveMap_RemapCount++;
$XaNotc1CatMoveMap_RemapName[$XaNotc1CatMoveMap_RemapCount] = "Fire Weapon";
$XaNotc1CatMoveMap_RemapCmd[$XaNotc1CatMoveMap_RemapCount] = "XaNotc1CatMoveMap_mouseFire";
$XaNotc1CatMoveMap_RemapCount++;
$XaNotc1CatMoveMap_RemapName[$XaNotc1CatMoveMap_RemapCount] = "Adjust Zoom";
$XaNotc1CatMoveMap_RemapCmd[$XaNotc1CatMoveMap_RemapCount] = "XaNotc1CatMoveMap_setZoomFov";
$XaNotc1CatMoveMap_RemapCount++;
$XaNotc1CatMoveMap_RemapName[$XaNotc1CatMoveMap_RemapCount] = "Toggle Zoom";
$XaNotc1CatMoveMap_RemapCmd[$XaNotc1CatMoveMap_RemapCount] = "XaNotc1CatMoveMap_toggleZoom";
$XaNotc1CatMoveMap_RemapCount++;
$XaNotc1CatMoveMap_RemapName[$XaNotc1CatMoveMap_RemapCount] = "Free Look";
$XaNotc1CatMoveMap_RemapCmd[$XaNotc1CatMoveMap_RemapCount] = "XaNotc1CatMoveMap_toggleFreeLook";
$XaNotc1CatMoveMap_RemapCount++;
$XaNotc1CatMoveMap_RemapName[$XaNotc1CatMoveMap_RemapCount] = "Switch 1st/3rd";
$XaNotc1CatMoveMap_RemapCmd[$XaNotc1CatMoveMap_RemapCount] = "XaNotc1CatMoveMap_toggleFirstPerson";
$XaNotc1CatMoveMap_RemapCount++;
$XaNotc1CatMoveMap_RemapName[$XaNotc1CatMoveMap_RemapCount] = "Suicide";
$XaNotc1CatMoveMap_RemapCmd[$XaNotc1CatMoveMap_RemapCount] = "XaNotc1CatMoveMap_suicide";
$XaNotc1CatMoveMap_RemapCount++;
$XaNotc1CatMoveMap_RemapName[$XaNotc1CatMoveMap_RemapCount] = "Launch Explosive Disc";
$XaNotc1CatMoveMap_RemapCmd[$XaNotc1CatMoveMap_RemapCount] = "XaNotc1CatMoveMap_launchExplosiveDisc";
$XaNotc1CatMoveMap_RemapCount++;
$XaNotc1CatMoveMap_RemapName[$XaNotc1CatMoveMap_RemapCount] = "Launch Repel Disc";
$XaNotc1CatMoveMap_RemapCmd[$XaNotc1CatMoveMap_RemapCount] = "XaNotc1CatMoveMap_launchRepelDisc";
$XaNotc1CatMoveMap_RemapCount++;
$XaNotc1CatMoveMap_RemapName[$XaNotc1CatMoveMap_RemapCount] = "Launch Razor Disc";
$XaNotc1CatMoveMap_RemapCmd[$XaNotc1CatMoveMap_RemapCount] = "XaNotc1CatMoveMap_launchRazorDisc";
$XaNotc1CatMoveMap_RemapCount++;
$XaNotc1CatMoveMap_RemapName[$XaNotc1CatMoveMap_RemapCount] = "Fire B.O.U.N.C.E.";
$XaNotc1CatMoveMap_RemapCmd[$XaNotc1CatMoveMap_RemapCount] = "XaNotc1CatMoveMap_fireBounce";
$XaNotc1CatMoveMap_RemapCount++;

function XaNotcSettings1_CatControlsGui::onWake(%this)
{
   //echo("XaNotcSettings1_CatControlsGui::onWake");
   
   if($InGuiEditor)
      return;

   %this-->MouseSensitivity.value = $pref::Input::LinkMouseSensitivity;
   %this.fillRemapList();
}

function XaNotcSettings1_CatControlsGui::onSleep(%this)
{
   //echo("XaNotcSettings1_CatControlsGui::onSleep");

   // write out the control config into the rw config file
   XaNotc1CatMoveMap.save("notc/client/settings/XaNotc1CatMoveMap.cs");
}

function XaNotcSettings1_CatControlsGui::fillRemapList( %this )
{
   %remapList = %this-->RemapList;

	%remapList.clear();
   for ( %i = 0; %i < $XaNotc1CatMoveMap_RemapCount; %i++ )
      %remapList.addRow( %i, XaNotcSettings1_CatControls_buildFullMapString( %i ) );
}

function XaNotcSettings1_CatControlsGui::doRemap( %this )
{
   %remapList = %this-->RemapList;

	%selId = %remapList.getSelectedId();
   %name = $XaNotc1CatMoveMap_RemapName[%selId];

	XaNotcSettings1_Controls_RemapDlg-->OptRemapText.setValue( "Re-bind \"" @ %name @ "\" to..." );
	XaNotcSettings1_Controls_RemapInputCtrl.index = %selId;
   XaNotcSettings1_Controls_RemapInputCtrl.inputEventCallback = "XaNotcSettings1_CatControls_onRemapCtrlInputEvent";
	Canvas.pushDialog(XaNotcSettings1_Controls_RemapDlg);
}

function XaNotcSettings1_CatControls_onRemapCtrlInputEvent(%device, %action)
{
   //error( "** onInputEvent called - device = " @ %device @ ", action = " @ %action @ " **" );
   Canvas.popDialog(XaNotcSettings1_Controls_RemapDlg);
   
   %index = XaNotcSettings1_Controls_RemapInputCtrl.index;

   // Test for the reserved keystrokes:
   if ( %device $= "keyboard" )
   {
      // Cancel...
      if ( %action $= "escape" )
      {
         // Do nothing...
         return;
      }
   }

   %cmd  = $XaNotc1CatMoveMap_RemapCmd[%index];
   %name = $XaNotc1CatMoveMap_RemapName[%index];

   // Grab the friendly display name for this action
   // which we'll use when prompting the user below.
   %mapName = XaNotcSettings1_CatControls_getMapDisplayName( %device, %action );

   // Get the current command this action is mapped to.
   %prevMap = XaNotc1CatMoveMap.getCommand( %device, %action );

   // If nothing was mapped to the previous command
   // mapping then it's easy... just bind it.
   if ( %prevMap $= "" )
   {
      XaNotcSettings1_CatControls_unbindExtraActions( %cmd, 1 );
      XaNotc1CatMoveMap.bind( %device, %action, %cmd );
      optionsDlg-->RemapList.setRowById( %index, XaNotcSettings1_CatControls_buildFullMapString( %index ) );
      return;
   }

   // If the previous command is the same as the
   // current then they hit the same input as what
   // was already assigned.
   if ( %prevMap $= %cmd )
   {
      XaNotcSettings1_CatControls_unbindExtraActions( %cmd, 0 );
      XaNotc1CatMoveMap.bind( %device, %action, %cmd );
      optionsDlg-->RemapList.setRowById( %index, XaNotcSettings1_CatControls_buildFullMapString( %index ) );
      return;
   }

   // Look for the index of the previous mapping.
   %prevMapIndex = XaNotcSettings1_CatControls_findRemapCmdIndex( %prevMap );

   // If we get a negative index then the previous
   // mapping was to an item that isn't included in
   // the mapping list... so we cannot unmap it.
   if ( %prevMapIndex == -1 )
   {
      MessageBoxOK( "Remap Failed", "\"" @ %mapName @ "\" is already bound to a non-remappable command!" );
      return;
   }

   // Setup the forced remapping callback command.
   %callback = "XaNotcSettings1_CatControls_redoMapping(" @ %device @ ", \"" @ %action @ "\", \"" @
                              %cmd @ "\", " @ %prevMapIndex @ ", " @ %index @ ");";

   // Warn that we're about to remove the old mapping and
   // replace it with another.
   %prevCmdName = $XaNotc1CatMoveMap_RemapName[%prevMapIndex];
   MessageBoxYesNo( "Warning",
      "\"" @ %mapName @ "\" is already bound to \""
      @ %prevCmdName @ "\"!\nDo you wish to replace this mapping?",
       %callback, "" );
}

function XaNotcSettings1_CatControls_getMapDisplayName( %device, %action )
{
	if ( %device $= "keyboard" )
		return( %action );
	else if ( strstr( %device, "mouse" ) != -1 )
	{
		// Substitute "mouse" for "button" in the action string:
		%pos = strstr( %action, "button" );
		if ( %pos != -1 )
		{
			%mods = getSubStr( %action, 0, %pos );
			%object = getSubStr( %action, %pos, 1000 );
			%instance = getSubStr( %object, strlen( "button" ), 1000 );
			return( %mods @ "mouse" @ ( %instance + 1 ) );
		}
		else
			error( "Mouse input object other than button passed to getDisplayMapName!" );
	}
	else if ( strstr( %device, "joystick" ) != -1 )
	{
		// Substitute "joystick" for "button" in the action string:
		%pos = strstr( %action, "button" );
		if ( %pos != -1 )
		{
			%mods = getSubStr( %action, 0, %pos );
			%object = getSubStr( %action, %pos, 1000 );
			%instance = getSubStr( %object, strlen( "button" ), 1000 );
			return( %mods @ "joystick" @ ( %instance + 1 ) );
		}
		else
	   {
	      %pos = strstr( %action, "pov" );
         if ( %pos != -1 )
         {
            %wordCount = getWordCount( %action );
            %mods = %wordCount > 1 ? getWords( %action, 0, %wordCount - 2 ) @ " " : "";
            %object = getWord( %action, %wordCount - 1 );
            switch$ ( %object )
            {
               case "upov":   %object = "POV1 up";
               case "dpov":   %object = "POV1 down";
               case "lpov":   %object = "POV1 left";
               case "rpov":   %object = "POV1 right";
               case "upov2":  %object = "POV2 up";
               case "dpov2":  %object = "POV2 down";
               case "lpov2":  %object = "POV2 left";
               case "rpov2":  %object = "POV2 right";
               default:       %object = "??";
            }
            return( %mods @ %object );
         }
         else
            error( "Unsupported Joystick input object passed to getDisplayMapName!" );
      }
	}

	return( "??" );
}

function XaNotcSettings1_CatControls_buildFullMapString( %index )
{
   %name       = $XaNotc1CatMoveMap_RemapName[%index];
   %cmd        = $XaNotc1CatMoveMap_RemapCmd[%index];

   %temp = XaNotc1CatMoveMap.getBinding( %cmd );
   if ( %temp $= "" )
      return %name TAB "";

   %mapString = "";

   %count = getFieldCount( %temp );
   for ( %i = 0; %i < %count; %i += 2 )
   {
      if ( %mapString !$= "" )
         %mapString = %mapString @ ", ";

      %device = getField( %temp, %i + 0 );
      %object = getField( %temp, %i + 1 );
      %mapString = %mapString @ XaNotcSettings1_CatControls_getMapDisplayName( %device, %object );
   }

   return %name TAB %mapString;
}

function XaNotcSettings1_CatControls_redoMapping( %device, %action, %cmd, %oldIndex, %newIndex )
{
	//%actionMap.bind( %device, %action, $XaNotc1CatMoveMap_RemapCmd[%newIndex] );
	XaNotc1CatMoveMap.bind( %device, %action, %cmd );

   %remapList = XaNotcSettings1_CatControlsGui-->RemapList;
	%remapList.setRowById( %oldIndex, XaNotcSettings1_CatControls_buildFullMapString( %oldIndex ) );
	%remapList.setRowById( %newIndex, XaNotcSettings1_CatControls_buildFullMapString( %newIndex ) );
}

function XaNotcSettings1_CatControls_findRemapCmdIndex( %command )
{
	for ( %i = 0; %i < $XaNotc1CatMoveMap_RemapCount; %i++ )
	{
		if ( %command $= $XaNotc1CatMoveMap_RemapCmd[%i] )
			return( %i );
	}
	return( -1 );
}

/// This unbinds actions beyond %count associated to the
/// particular XaNotc1CatMoveMap %commmand.
function XaNotcSettings1_CatControls_unbindExtraActions( %command, %count )
{
   %temp = XaNotc1CatMoveMap.getBinding( %command );
   if ( %temp $= "" )
      return;

   %count = getFieldCount( %temp ) - ( %count * 2 );
   for ( %i = 0; %i < %count; %i += 2 )
   {
      %device = getField( %temp, %i + 0 );
      %action = getField( %temp, %i + 1 );

      XaNotc1CatMoveMap.unbind( %device, %action );
   }
}

function XaNotcSettings1_CatControls_restoreDefaultMappings()
{
   XaNotc1CatMoveMap.delete();
   exec("notc/client/settings/XaNotc1CatMoveMap.cs");
   optionsDlg.fillRemapList();
}

function XaNotcSettings1_CatControls_MouseSetSensitivity(%value)
{
   $pref::Input::LinkMouseSensitivity = %value;
}

